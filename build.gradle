plugins {
    id 'groovy'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.10.2'
}

repositories {
    jcenter()
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.13'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.25'
    implementation group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.1'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['.']
            exclude(['**/test/**', 'gradle/**'])
        }
    }
}

compileGroovy {
    groovyOptions.configurationScript = file('gradle/config.groovy')
}

ext.jacoco = [
    coverageThresholds: [
        'YamlValidator': [
            'BRANCH': 0.67,  // DO NOT CHANGE unless there is a huge improvement. The values fluctuate in CI
            'COMPLEXITY': 0.66, // DO NOT CHANGE unless there is a huge improvement. The values fluctuate in CI
            'INSTRUCTION': 0.92,
            'LINE': 0.89
        ],
        'YamlValidator._validateYamlFiles_closure1': [
            'BRANCH': 0.75,
            'COMPLEXITY': 0.75,
            'INSTRUCTION': 0.93,
            'LINE': 0.83
        ]
    ]
]
apply from: '../gradle-includes/checks.gradle'